plugins {
    id "org.grooscript.conversion" version "1.1.1" //<1>
    id "org.asciidoctor.convert" version "1.5.2"
}

apply plugin: 'idea'
apply plugin: 'groovy'

ext {
    groovyVersion = '2.4.3'
    grooscriptVersion = '1.1.1'
    imagesDir = 'img'
    docImagesDir = 'img'
    jsDir = 'js'
}

repositories {
    mavenCentral()
}

dependencies { //<3>
    compile group: 'org.codehaus.groovy', name: 'groovy', version: groovyVersion
    compile group: 'org.codehaus.groovy', name: 'groovy-test', version: groovyVersion
    compile ("org.grooscript:grooscript:${grooscriptVersion}") {
        exclude module: 'groovy'
        exclude module: 'groovy-json'
    }
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
    testCompile 'cglib:cglib-nodep:2.2.2'
}

grooscript {
    source = ['src/main/groovy/Execute.groovy',
              'src/main/groovy/nodejs/NodeJs.groovy',
              'src/main/groovy/colors/JsColors.groovy'
    ] //<4>
    destination = 'js' //<5>
    classPath = ['src/main/groovy'] //<7>
    initialText = '//Grooscript converted file'
}

task convertReact(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/react.groovy', 'src/main/groovy/react/TodoApp.groovy']
    destination = 'src/main/webapp/js'
    classPath = ['src/main/groovy']
}

task convertPaint(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/paint/Functions.groovy', 'src/main/groovy/paint/Draw.groovy']
    destination = 'src/main/webapp/js'
    classPath = ['src/main/groovy']
}

task convertAsciidoctor(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/asciidoctor/AdocLive.groovy', 'src/main/groovy/startLiveAdoc.groovy']
    destination = 'src/main/webapp/js'
    classPath = ['src/main/groovy']
}

task convertGame2048(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/game']
    destination = 'src/main/webapp/js'
    classPath = ['src/main/groovy']
}

task convertCountries(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/countries']
    destination = 'src/main/webapp/js'
    classPath = ['src/main/groovy']
}

task convertChatServer(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/chat/NodeServer.groovy', 'src/main/groovy/startServer.groovy']
    destination = 'js'
    classPath = ['src/main/groovy']
}

task convertChatClient(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/chat/Client.groovy']
    destination = 'src/main/webapp/js'
    classPath = ['src/main/groovy']
}

task chatTemplates(type: org.grooscript.gradle.TemplatesTask) {
    templatesPath = 'src/main/webapp/templates'
    templates = ['join.gtpl', 'left.gtpl', 'message.gtpl', 'index.gtpl']
    destinationFile = 'src/main/webapp/js/gstemplates.js'
    //classPath
}

task chat << {
    println 'Chat converted.'
}

chat.dependsOn chatTemplates, convertChatClient, convertChatServer

task convertStars(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/raphael/Stars.groovy']
    destination = 'src/main/webapp/js'
    classPath = ['src/main/groovy']
}

task convertSnap(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/snap/', 'src/main/groovy/snap.groovy']
    destination = 'src/main/webapp/js'
    classPath = ['src/main/groovy']
}

task convertFirebase(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/firebase/', 'src/main/groovy/firebaseAction.groovy']
    destination = 'src/main/webapp/js'
    classPath = ['src/main/groovy']
}

task convertThree(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/three/']
    destination = 'src/main/webapp/js'
    classPath = ['src/main/groovy']
}

task convertAngular(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/angular']
    destination = 'src/main/webapp/js'
    classPath = ['src/main/groovy']
}

task convertRxJs(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/rxjs']
    destination = 'src/main/webapp/js'
    classPath = ['src/main/groovy']
}

task convertJsonData(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/authors.groovy']
    destination = 'js'
    initialText = "var gs = require('grooscript');"
}

task convertAll << {
    println 'All converted.'
}

task showInfo << {
    println """
For Node demos, you need Node.js installed and run as sudo :> npm install

- Node.js console demo run:> node ${jsDir}/colors.js
- Chat Node.js demo. Run server with:> node ${jsDir}/startChat.js then browse to: http://localhost:5000/
- Json server. You need json server installed with:> npm install -g json-server
               Later run the server with:> json-server js/authors.js

- For react.js native demo, use task convertReactNative, and take a look at js/awesome.js

Browser demos:

- Asciidoctor live conversion demo file://${project.projectDir}/src/main/webapp/asciidoctor.html
- Pollock's paint demo file://${project.projectDir}/src/main/webapp/bezier.html
- Firebase demo file://${project.projectDir}/src/main/webapp/firebase.html
- 2048 game demo file://${project.projectDir}/src/main/webapp/game.html
- React demo file://${project.projectDir}/src/main/webapp/react.html
- Sigma map graph demo file://${project.projectDir}/src/main/webapp/sigma.html
- Snap SVG Clock demo file://${project.projectDir}/src/main/webapp/snapsvg.html
- Stars demo file://${project.projectDir}/src/main/webapp/stars.html
- Three.js demo file://${project.projectDir}/src/main/webapp/three.html
- Angular.js demo file://${project.projectDir}/src/main/webapp/angular.html
- Angular.js second approach demo file://${project.projectDir}/src/main/webapp/angularTwo.html
- RxJS demo file://${project.projectDir}/src/main/webapp/autocomplete.html
- RxJS demo two file://${project.projectDir}/src/main/webapp/autocompleteTwo.html
"""
}

convertAll.dependsOn convert, convertPaint, convertAsciidoctor, convertReact, convertGame2048,
        convertCountries, chat, convertStars, convertSnap, convertFirebase, showInfo, convertThree,
        convertAngular, convertRxJs, convertJsonData

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

task convertReactNative(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/reactNative/awesome.groovy']
    destination = 'js'
    classPath = ['src/main/groovy']
    mainContextScope = ['React', 'StyleSheet', 'View', 'Text']
    initialText = '''//Grooscript example using reactjs native to create ios app
//Starting react native getting started: http://facebook.github.io/react-native/docs/getting-started.html#content
//and replace index.ios.js with this file
'use strict';

var React = require('react-native');
var {
    AppRegistry,
    StyleSheet,
    Text,
    Image,
    View,
} = React;

var gs = require('grooscript');
'''
    finalText = 'AppRegistry.registerComponent(\'AwesomeProject\', () => AwesomeProject);'
}

asciidoctor {
    sourceDir = project.file 'doc'
    attributes 'grooscript-version': grooscriptVersion,
            author: 'Jorge Franco',
            jsDir: project.file('js').path,
            buildFile: project.file('./build.gradle').path,
            sourcesDir: project.file('src/main/groovy').path,
            templatesDir: project.file('src/main/webapp/templates').path,
            email: 'grooscript@gmail.com',
            'source-highlighter': 'prettify',
            imagesdir: docImagesDir,
            icons: 'font',
            'index-file': 'index.html'
    resources {
        from imagesDir
        into docImagesDir
    }
}

