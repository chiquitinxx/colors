// tag::conf[]
plugins {
    id "org.grooscript.conversion" version "1.2.0" //<1>
    id "org.asciidoctor.convert" version "1.5.2"
}

apply plugin: 'idea'
apply plugin: 'groovy'

ext {
    groovyVersion = '2.4.4'
    gradleCurrentVersion = '2.5'
    grooscriptVersion = '1.2.0'
    imagesDir = 'img'
    docImagesDir = 'img'
    jsDir = 'js'
}

repositories {
    mavenCentral()
}

dependencies { //<2>
    compile group: 'org.codehaus.groovy', name: 'groovy', version: groovyVersion
    compile group: 'org.codehaus.groovy', name: 'groovy-json', version: groovyVersion
    compile group: 'org.codehaus.groovy', name: 'groovy-test', version: groovyVersion
    compile('io.reactivex:rxgroovy:1.0.0') {
        exclude module: 'groovy-all'
    }
    compile ("org.grooscript:grooscript:${grooscriptVersion}") {
        exclude module: 'groovy'
        exclude module: 'groovy-json'
    }
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
    testCompile 'cglib:cglib-nodep:2.2.2'
}

grooscript {
    source = ['src/main/groovy/Execute.groovy',
              'src/main/groovy/nodejs/NodeJs.groovy',
              'src/main/groovy/colors/JsColors.groovy'] //<3>
    destination = 'js' //<4>
    classpath = ['src/main/groovy'] //<5>
    initialText = '//Grooscript converted file'
}
// end::conf[]

task convertReact(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/react.groovy']
    destination = 'src/main/webapp/js'
    includeDependencies = true
}

task convertPaint(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/paint/Functions.groovy', 'src/main/groovy/paint/Draw.groovy']
    destination = 'src/main/webapp/js'
}

task convertAsciidoctor(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/asciidoctor/AdocLive.groovy', 'src/main/groovy/startLiveAdoc.groovy']
    destination = 'src/main/webapp/js'
}

task convertGame2048(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/game']
    destination = 'src/main/webapp/js'
}

task convertCountries(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/countries']
    destination = 'src/main/webapp/js'
}

task convertChatServer(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/chat/NodeServer.groovy', 'src/main/groovy/startServer.groovy']
    destination = 'js'
}

task convertChatClient(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/chat/Client.groovy']
    destination = 'src/main/webapp/js'
}

task chatTemplates(type: org.grooscript.gradle.TemplatesTask) {
    templatesPath = 'src/main/webapp/templates'
    templates = ['join.gtpl', 'left.gtpl', 'message.gtpl', 'index.gtpl']
    destinationFile = 'src/main/webapp/js/gstemplates.js'
}

task chat << {
    println 'Chat converted.'
}

chat.dependsOn chatTemplates, convertChatClient, convertChatServer

task convertStars(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/raphael/Stars.groovy']
    destination = 'src/main/webapp/js'
}

task convertSnap(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/snap/', 'src/main/groovy/snap.groovy']
    destination = 'src/main/webapp/js'
}

task convertFirebase(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/firebase/', 'src/main/groovy/firebaseAction.groovy']
    destination = 'src/main/webapp/js'
}

task convertThree(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/three/']
    destination = 'src/main/webapp/js'
}

task convertAngular(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/angular']
    destination = 'src/main/webapp/js'
}

task convertRxJs(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/rxjs']
    destination = 'src/main/webapp/js'
}

task convertJsonData(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/authors.groovy']
    destination = 'js'
    initialText = "var gs = require('grooscript');"
}

task convertGoogleMaps(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/maps', 'src/main/groovy/startMap.groovy']
    destination = 'src/main/webapp/js/map'
}

task convertJQueryRest(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/rest', 'src/main/groovy/jQueryRest.groovy']
    destination = 'src/main/webapp/js/jqueryrest'
}

task convertDualRx(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/dualrx', 'src/main/groovy/observeRx.groovy']
    destination = 'js'
}

task convertAll << {
    println 'All converted.'
}

task showInfo << {
    println """
For Node demos, you need Node.js installed and run as sudo :> npm install

- Node.js console demo run:> node ${jsDir}/colors.js
- Chat Node.js demo. Run server with:> node startChat.js then browse to: http://localhost:5000/
- Json server. You need json server installed with:> npm install -g json-server
               Later run the server with:> json-server js/authors.js

- For react.js native demo, use task convertReactNative, and take a look at js/awesome.js

Browser demos:

- Asciidoctor live conversion demo file://${project.projectDir}/src/main/webapp/asciidoctor.html
- Pollock's paint demo file://${project.projectDir}/src/main/webapp/bezier.html
- Firebase demo file://${project.projectDir}/src/main/webapp/firebase.html
- 2048 game demo file://${project.projectDir}/src/main/webapp/game.html
- React demo file://${project.projectDir}/src/main/webapp/react.html
- Sigma map graph demo file://${project.projectDir}/src/main/webapp/sigma.html
- Snap SVG Clock demo file://${project.projectDir}/src/main/webapp/snapsvg.html
- Stars demo file://${project.projectDir}/src/main/webapp/stars.html
- Three.js demo file://${project.projectDir}/src/main/webapp/three.html
- Angular.js demo file://${project.projectDir}/src/main/webapp/angular.html
- Angular.js second approach demo file://${project.projectDir}/src/main/webapp/angularTwo.html
- RxJS demo file://${project.projectDir}/src/main/webapp/autocomplete.html
- RxJS demo two file://${project.projectDir}/src/main/webapp/autocompleteTwo.html
- Google maps demo file://${project.projectDir}/src/main/webapp/maps.html
"""
}

convertAll.dependsOn convert, convertPaint, convertAsciidoctor, convertReact, convertGame2048,
        convertCountries, chat, convertStars, convertSnap, convertFirebase, showInfo, convertThree,
        convertAngular, convertRxJs, convertJsonData, convertGoogleMaps

task wrapper(type: Wrapper) {
    gradleVersion = gradleCurrentVersion
}

task convertReactNative(type: org.grooscript.gradle.ConvertTask) {
    source = ['src/main/groovy/reactNative/awesome.groovy']
    destination = 'js'
    mainContextScope = ['React', 'StyleSheet', 'View', 'Text']
    initialText = '''//Grooscript example using reactjs native to create ios app
//Starting react native getting started: http://facebook.github.io/react-native/docs/getting-started.html#content
//and replace index.ios.js with this file
'use strict';

var React = require('react-native');
var {
    AppRegistry,
    StyleSheet,
    Text,
    Image,
    View,
} = React;

var gs = require('grooscript');
'''
    finalText = 'AppRegistry.registerComponent(\'AwesomeProject\', () => AwesomeProject);'
}

asciidoctor {
    sourceDir = project.file 'doc'
    attributes 'grooscript-version': grooscriptVersion,
            author: 'Jorge Franco',
            jsDir: project.file('js').path,
            currentDir: project.file('.').path,
            buildFile: project.file('./build.gradle').path,
            sourcesDir: project.file('src/main/groovy').path,
            webDir: project.file('src/main/webapp').path,
            templatesDir: project.file('src/main/webapp/templates').path,
            gradleVersion: gradleCurrentVersion,
            email: 'grooscript@gmail.com',
            'source-highlighter': 'prettify',
            imagesdir: docImagesDir,
            icons: 'font',
            'index-file': 'index.html'
    resources {
        from imagesDir
        into docImagesDir
    }
}

